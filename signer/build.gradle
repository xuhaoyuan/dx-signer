plugins {
    id 'java'
    id 'idea'
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'https://maven.aliyun.com/repository/jcenter' }
}

dependencies {
    implementation fileTree(dir: 'lib', include: ['*.jar'])
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'com.meituan.android.walle:payload_writer:1.1.7'
    implementation 'org.json:json:20231013'
    implementation 'org.slf4j:slf4j-simple:1.7.30'
    implementation 'com.intellij:forms_rt:7.0.3'
}

tasks.register('fatJar', Jar) {
    dependsOn jar
    archiveBaseName.convention('dx-signer')

    def deps = configurations.runtimeClasspath

    def depClasses = { deps.collect { it.isDirectory() ? it : zipTree(it) } }

    from(depClasses) {
        exclude 'META-INF/*.MF'
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.RSA'
        exclude 'META-INF/*.DSA'
        exclude '**/*.html'
        exclude 'META-INF/maven/**'
    }

    manifest {
        attributes 'Implementation-Title': 'dx signer',
                'Main-Class': 'dx.signer.UX'
    }

    from(sourceSets.main.output)
}
